#include "symbols.fst"

% Numbers represented in text format has limited vocabulary because of obvious reason
% The lexicon for that is given below.
$half$ = അര <half>
$n1$ = ഒന്ന് | ഒന്നു്

$n0$ = പൂജ്യം <zero>
$n2-9$ = (രണ്ട്|മൂന്ന്|നാല്|അഞ്ച്|ആറ്|ഏഴ്|എട്ട്|ഒമ്പത്)

% ones
$N1$ = ( $n1$ | $n2-9$ ) <ones>

$n10-90$ = പത്ത് |\
	{രണ്ട് <ones> പത്ത്} :{ഇരുപത്} |\
	{മൂന്ന് <ones> പത്ത്} : {മുപ്പത്} |\
	{നാല് <ones> പത്ത്} :{നാല്പത്} |\
	{അഞ്ച് <ones> പത്ത്} : {അമ്പത്} |\
	{ആറ് <ones> പത്ത് } : {അറുപത്} |\
	{ഏഴ് <ones> പത്ത്} :{എഴുപത്} |\
	{എട്ട് <ones> പത്ത്} : {എൺപത്} |\
	{ഒമ്പത് <ones> പത്ത് } : {തൊണ്ണൂറ്}
$n11-19$ = { പത്ത് <tens> ഒന്ന്} : {പതിനൊന്ന്} |\
	{ പത്ത് <tens> രണ്ട്} : {പന്ത്രണ്ട്} |\
	{ പത്ത് <tens> മൂന്ന്} : {പതിമൂന്ന്} |\
	{ പത്ത് <tens> നാല്} : {പതിനാല്} |\
	{ പത്ത് <tens> അഞ്ച്} : {പതിനഞ്ച്} |\
	{ പത്ത് <tens> ആറ്} : {പതിനാറ്} |\
	{ പത്ത് <tens> ഏഴ്} : {പതിനേഴ്} |\
	{ പത്ത് <tens> എട്ട്} : {പതിനെട്ട്} |\
	{ പത്ത് <tens> ഒമ്പത്} : {പത്തൊമ്പത്}

$n20-90-compound$ = {രണ്ട് <ones> പത്ത്} : {ഇരുപത്തി} |\
	{മൂന്ന് <ones> പത്ത്} : {മുപ്പത്തി} |\
	{നാല് <ones> പത്ത്} : {നാല്പത്തി} |\
	{നാല് <ones> പത്ത്} : {നാൽപത്തി} |\
	{അഞ്ച് <ones> പത്ത്} : {അമ്പത്തി} |\
	{അഞ്ച് <ones> പത്ത്} : {അൻപത്തി} |\
	{ആറ് <ones> പത്ത് } : {അറുപത്തി} |\
	{ഏഴ് <ones> പത്ത്} : {എഴുപത്തി} |\
	{എട്ട് <ones> പത്ത്} : {എൺപത്തി} |\
	{എട്ട് <ones> പത്ത്} : {എമ്പത്തി} |\
	{ഒമ്പത് <ones> പത്ത് } : {തൊണ്ണൂറ്റി}

% tens
$N10$ = ( $n10-90$ <tens> ) | \
	( ( $n11-19$ <ones> ) | ( $n20-90-compound$ <tens> ) $N1$ )

% hundreds
$n100-900$ = നൂറ് |\
	{രണ്ട് <ones> നൂറ്} : {ഇരുന്നൂറ്} |\
	{മൂന്ന് <ones> നൂറ്} : {മുന്നൂറ്} |\
	{നാല് <ones> നൂറ്} : {നാനൂറ്} |\
	{അഞ്ച് <ones> നൂറ്} : {അഞ്ഞൂറ്} |\
	{ആറ് <ones> നൂറ്} : {അറുന്നൂറ്} |\
	{ഏഴ് <ones> നൂറ്} : {എഴുന്നൂറ്} |\
	{എട്ട് <ones> നൂറ്} : {എണ്ണൂറ്} |\
	{ഒമ്പത് <ones> നൂറ്} : {തൊള്ളായിരം}

$n100-900-compound$ = {നൂറ്} : {നൂറ്റി} |\
	{രണ്ട് <ones> നൂറ്} : {ഇരുനൂറ്റി} |\
	{മൂന്ന് <ones> നൂറ്} : {മുന്നൂറ്റി} |\
	{നാല് <ones> നൂറ്} : {നാനൂറ്റി} |\
	{അഞ്ച് <ones> നൂറ്} : {അഞ്ഞൂറ്റി} |\
	{ആറ് <ones> നൂറ്} : {അറുനൂറ്റി} |\
	{ഏഴ് <ones> നൂറ്} : {എഴുനൂറ്റി} |\
	{എട്ട് <ones> നൂറ്} : {എണ്ണൂറ്റി} |\
	{ഒമ്പത് <ones> നൂറ്} : {തൊള്ളായിരത്തി}

$N100$ = ( $n100-900$ <hundreds> ) |\
	( $n100-900-compound$ <hundreds> ($N10$|$N1$) )

% thousands
$n1k$ = ആയിരം
$N1K-thousands-exeptions$ = { അഞ്ച് <ones> ആയിരം} : {അയ്യായിരം} |\
	{ മൂന്ന് <ones> ആയിരം } : {മൂന്നായിരം} |\
	{ മൂന്ന് <ones> ആയിരം } : {മുവ്വായിരം} |\
	{ എട്ട് <ones> ആയിരം } : {എണ്ണായിരം} |\
	{ പത്ത് <ones> ആയിരം } : {പതിനായിരം} |\
	{ പത്ത് <tens> ആയിരം } : {പത്തായിരം} |\
	{ രണ്ട് <ones> പത്ത് <tens> ആയിരം } : {ഇരുപതിനായിരം} |\
	{ മൂന്ന് <ones> പത്ത് <tens> ആയിരം } : {മുപ്പതിനായിരം} |\
	{ നാല് <ones> പത്ത് <tens> ആയിരം } : {നാല്പതിനായിരം} |\
	{ അഞ്ച് <ones> പത്ത് <tens> ആയിരം } : {അമ്പതിനായിരം} |\
	{ ആറ് <ones> പത്ത് <tens> ആയിരം } : {അറുപതിനായിരം} |\
	{ ഏഴ് <ones> പത്ത് <tens> ആയിരം } : {എഴുപതിനായിരം} |\
	{ എട്ട് <ones> പത്ത് <tens> ആയിരം } : {എൺപതിനായിരം} |\
	{ ഒമ്പത് <ones> പത്ത് <tens> ആയിരം } : {തൊണ്ണൂറായിരം}

$N1K$ = ( ( $N10$|$N1$ )? $n1k$ | $N1K-thousands-exeptions$ ) <thousands>
$n1k-compound$ = {ആയിരം}: {ആയിരത്തി} |\
	{ മൂന്ന് <ones> ആയിരം } : {മുവ്വായിരത്തി} |\
	{ അഞ്ച് <ones> ആയിരം} : {അയ്യായിരത്തി} |\
	{ എട്ട് <ones> ആയിരം } : {എണ്ണായിരത്തി} |\
	{ പത്ത് <tens> ആയിരം } : {പതിനായിരത്തി} |\
	{ രണ്ട് <ones> പത്ത് <tens> ആയിരം } : {ഇരുപതിനായിരത്തി} |\
	{ മൂന്ന് <ones> പത്ത് <tens> ആയിരം } : {മുപ്പതിനായിരത്തി} |\
	{ നാല് <ones> പത്ത് <tens> ആയിരം } : {നാല്പതിനായിരത്തി} |\
	{ അഞ്ച് <ones> പത്ത് <tens> ആയിരം } : {അമ്പതിനായിരത്തി} |\
	{ ആറ് <ones> പത്ത് <tens> ആയിരം } : {അറുപതിനായിരത്തി} |\
	{ ഏഴ് <ones> പത്ത് <tens> ആയിരം } : {എഴുപതിനായിരത്തി} |\
	{ എട്ട് <ones> പത്ത് <tens> ആയിരം } : {എൺപതിനായിരത്തി} |\
	{ ഒമ്പത് <ones> പത്ത് <tens> ആയിരം } : {തൊണ്ണൂറായിരത്തി}

$N1K-ones-tens-hundreds$ = ( $N10$|$N1$ )? $n1k-compound$ <thousands> ($N100$|$N10$|$N1$)
$N1K$ = $N1K$ | $N1K-ones-tens-hundreds$

$n1l$ = ലക്ഷം
$n1l-compound$ = {ലക്ഷം} : {ലക്ഷത്തി}
$N1L$ = (( $N1$|$N10$|$N100$|$N1K$ )? $n1l$ <lakhs>) |\
	(( $N1$|$N10$|$N100$|$N1K$)? $n1l-compound$ <lakhs> ($N1K$|$N100$|$N10$|$N1$))

$n1c$ = കോടി
$N1C$ = ( $N1$|$N10$|$N100$|$N1K$|$N1L$ )? $n1c$ <crores> ($N1L$|$N1K$|$N100$|$N10$|$N1$)?

$numbers$ = ( $n0$ | $N1$ | $N10$ | $N100$ | $N1K$ | $N1L$ | $N1C$ ) $half$?

<LB> $numbers$ <cardinal><RB>
